//file:noinspection DependencyNotationArgument
apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics' //add for firebase crashlytics
apply plugin: 'com.google.dagger.hilt.android'
// These plugins are just for oversea flavor
//apply plugin: 'com.google.gms.google-services'//add for firebase service

def ext = rootProject.ext
def androidConfig = ext.android
def libraries = ext.libraries

android {
    compileSdkVersion = androidConfig.compileSdkVersion
    defaultConfig {
        applicationId 'com.yaya.life'
        minSdkVersion androidConfig.minSdkVersion
        targetSdkVersion androidConfig.targetSdkVersion
        versionCode androidConfig.versionCode
        versionName androidConfig.versionName
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildFeatures {
        viewBinding true
        aidl true
        buildConfig true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }

        release {
            applicationIdSuffix '.release'
            versionNameSuffix '-release'
            // Enables code shrinking, obfuscation, and optimization for only
            // project's release build type.
            minifyEnabled true

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources true

            // Includes the default ProGuard rules files that are packaged with
            // the Android Gradle plugin. To learn more, go to the section about
            // R8 configuration files.
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions = ['world']
    productFlavors {
        domestic {
            dimension 'world'
            applicationIdSuffix '.domestic'
            versionNameSuffix '-domestic'
        }

        oversea {
            dimension 'world'
            applicationIdSuffix '.oversea'
            versionNameSuffix '-oversea'
        }
    }

    // filter build variant
//    variantFilter { variant ->
//        def names = variant.flavors*.name
//
//        if (names.contains("domestic")) {
//            setIgnore(true)
//        }
//    }
    namespace 'com.yaya.life'
}

//if you want to add a dependency for a variant that combines a product flavor
// and a build type, then you must initialize the configuration name
// in the configurations block.
configurations {
    // Initializes a placeholder for the overseaDebugImplementation dependency
    // configuration.
    overseaDebugImplementation {}
}

dependencies {
    //common dependencies
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation libraries.kotlin_stdlib

    // local library
    implementation project(":player-lib")
    implementation project(':image')
    implementation project(':data')
    implementation project(':utils')

    //androidx
    implementation libraries.androidx_appcompat
    implementation libraries.androidx_core_ktx
    implementation libraries.androidx_constraintlayout
    implementation libraries.androidx_recyclerview
    implementation libraries.androidx_cardview
    implementation libraries.androidx_viewpager2
    implementation libraries.androidx_swipe_refresh
    implementation libraries.androidx_lifecycle_common_java8
    implementation libraries.androidx_fragment_ktx
    implementation libraries.androidx_activity_ktx

    //navigation
    implementation libraries.navigation_fragment
    implementation libraries.navigation_ui

    //google
    implementation libraries.google_material

    //hilt
    implementation libraries.hilt
    kapt libraries.hilt_compiler

    //oversea dependencies
    overseaImplementation platform(libraries.firebase_bom)
    overseaImplementation libraries.firebase_analytics
    overseaImplementation libraries.firebase_crashlytics
    overseaImplementation libraries.firebase_messaging

    //debug dependencies
    debugImplementation libraries.leakcanary
}

// Allow references to generated code
kapt {
    correctErrorTypes true
}
